grammar org.xtext.example.nml.NML  with org.eclipse.ocl.xtext.essentialocl.EssentialOCL //with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/ocl/2015/BaseCS" as base
import "http://www.eclipse.org/ocl/2015/Pivot" as pivot
import "http://www.eclipse.org/ocl/2015/EssentialOCLCS" as essentialocl

generate nML "http://www.xtext.org/example/nml/NML"

TopLevelCP:
	'module' name=Identifier '{'
		(ownedDomains+=DomainCP)+
		('pre' (UnrestrictedName)? '{' ownedPre+=StatementCS* '}')?
		('rule' UnrestrictedName ownedOctopus+=OctopusCP)*
		('rule' UnrestrictedName ownedOurs+=OursCP)*
		('rule' UnrestrictedName ownedTransformation+=TransferCP)* 
		('post' (UnrestrictedName)? '{' ownedPre+=StatementCS* '}')?
	'}' ;
	

DomainCP:
	'import' (name=ID ':')? nsURI=URI ';' 
	
;

OctopusCP:
	'mergeOctopus' ownedModels+=modelTypeCS 
	'with' ownedModels+=modelTypeCS (',' ownedModels+=modelTypeCS)*
	('withBase' ownedModels+=modelTypeCS)?
	'into' ownedModels+=modelTypeCS
	('extends' UnrestrictedName (',' UnrestrictedName)*)?
	'{'
		(ownedConditions+=StatementCS)+
	'}'
;

OursCP:
	'mergeOurs' ownedModels+=modelTypeCS 
	'with' ownedModels+=modelTypeCS (',' ownedModels+=modelTypeCS)*
	('withBase' ownedModels+=modelTypeCS ('exists_in' INT)?)?
	('priority' ownedOrders+=modelOrderCS (',' ownedOrders+=modelOrderCS)*)?
	'into' ownedModels+=modelTypeCS
	('extends' UnrestrictedName (',' UnrestrictedName)*)?
	'{'
		(ownedStatement+=StatementCS)+
	'}'
;

TransferCP:
	'transfer' ownedModels+=modelTypeCS 
	'from' ownedLists+=modelListCS
	'to' ownedModels+=modelTypeCS
	('extends' UnrestrictedName (',' UnrestrictedName)*)?
	'{'
		('guard' (('or'|'and')? ownedConditions+=ConditionCS)+)?
		(ownedStatement+=StatementCS)+
	'}'
;


modelTypeCS:
	name=Identifier ':' UnrestrictedName'!'UnrestrictedName
;

modelOrderCS:
	name=Identifier ':' '[' UnrestrictedName (',' UnrestrictedName)* ']'
;

modelListCS:
	name=Identifier ':' '(' UnrestrictedName (',' UnrestrictedName)* ')'
;

StatementCS:
	('var')? (ownedElement+=FQN)? ('='|'::=')? (ownedElement+=FQN) ("." 'equivalent' '(' ')')? (('+'|'new') (ownedElement+=FQN))* ';'
;

ConditionCS:
	(ownedElement+=FQN)? ('>'|'<'|'='|'<>'|'!'|'>='|'=<')? (ownedElement+=FQN) 
;

FQN:
	(UnrestrictedName|ID) (("."|"->") (UnrestrictedName|ID) ('(')? (')')? )*
;

